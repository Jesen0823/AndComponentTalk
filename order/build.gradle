plugins {
    // 声明插件并设置 apply false（仅声明，不立即应用）
    alias(libs.plugins.kotlin.android) apply false
    alias(libs.plugins.android.application) apply false
    alias(libs.plugins.android.library) apply false
}

def isDebug = project.property("isDebug").toBoolean()
// 自定义选项
def mVersions = rootProject.ext.mVersions
def mDencies = rootProject.ext.mDencies
def mIds = rootProject.ext.mIds
def host_url = rootProject.ext.host_url

// 根据 isDebug 条件应用 Application 或 Library 插件
if (isDebug) {
    apply plugin: libs.plugins.android.application.get().pluginId
} else {
    apply plugin: libs.plugins.android.library.get().pluginId
}
// 必须应用 Kotlin Android 插件（无论应用/库模块）
apply plugin: libs.plugins.kotlin.android.get().pluginId


android {
    namespace = mIds.order
    compileSdkVersion = mVersions.compileSdk

    defaultConfig {
        if (isDebug) {
            applicationId mIds.order
        }
        minSdk mVersions.minSdk
        targetSdk mVersions.targetSdk
        versionCode mVersions.versionCode
        versionName mVersions.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // 这个方法接收三个非空的参数，第一个：确定值的类型，第二个：指定key的名字，第三个：传值（必须是String）
        // 为什么需要定义这个？因为src代码中有可能需要用到跨模块交互，如果是组件化模块显然不行
        // 切记：不能在android根节点，只能在defaultConfig或buildTypes节点下
        buildConfigField("boolean", "isDebug", String.valueOf(isDebug))

        // 在gradle文件中配置选项参数值（用于APT传参接收）
        // 切记：必须写在defaultConfig节点下
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.name, packageNameForAPT: packageNameForAPT]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            if (isDebug) {
                // 如果是组件化模式，需要单独运行时
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            } else {
                // 集成化模式，整个项目打包apk
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    // release 时 debug 目录下文件不需要合并到主工程
                    exclude '**/debug/**'
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        buildConfig = true // 显式开启BuildConfig生成（AGP 8.x 默认开启）
    }
}

dependencies {
    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.material
    implementation libs.androidx.activity
    implementation libs.androidx.constraintlayout

    mDencies.each { k, v -> implementation v }

    // apt注解
    implementation project(path: ':component_annotation_lib')
    // apt注解处理器
    annotationProcessor project(path: ':component_compiler_lib')

    implementation project(path: ':util_lib')
    implementation project(path: ':common_lib')
}